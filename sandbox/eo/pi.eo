+package sandbox
+alias stdout org.eolang.io.stdout
+alias sprintf org.eolang.txt.sprintf

[args...] > apppi
  stdout > @
    sprintf
      "%f"
      compute.
        pi
        (args.get 0).toInt

[] > pi
  3.1415926535 > @

  [n] > compute
    mul. > @
      4.0
      subCompute
        1.0
        3.0
        1
        add.
          n
          1

    [accum divider i n] > subCompute
      if. > @
        less.
          i
          n
        subCompute
          add.
            accum
            div.
              1.0
              if.
                eq.
                  mod.
                    i
                    2
                  0
                divider
                divider.neg
          add.
            divider
            2.0
          add.
            i
            1
          n
        accum
